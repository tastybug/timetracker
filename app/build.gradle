apply plugin: 'com.android.application'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().substring(0, 8).trim()
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId "com.tastybug.timetracker"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 102
        versionName "1.02"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    lintOptions {
        abortOnError true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
    signingConfigs {
        release {
//            storeFile file("../timetracker.keystore")
//            storePassword TIMETRACKER_RELEASE_STORE_PASSWORD // ~/.gradle/gradle.properties
//            keyAlias "timetracker"
//            keyPassword TIMETRACKER_RELEASE_KEY_PASSWORD // ~/.gradle/gradle.properties
        }
    }
    buildTypes {
        debug {
            buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
            resValue "bool", "test_data_generator_accessible", 'true'
        }
        release {
            buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
            resValue "bool", "test_data_generator_accessible", 'false'
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"),
                    "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation 'com.google.guava:guava:20.0' // higher versions require API 24+
    implementation 'com.squareup:otto:1.3.4'
    implementation 'net.danlew:android.joda:2.9.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'commons-io:commons-io:2.5'
    implementation 'ch.acra:acra:4.9.2'
    implementation('com.opencsv:opencsv:3.9') {
        exclude module: 'commons-logging'
    }
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.assertj:assertj-guava:3.1.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.13'
    testImplementation 'org.robolectric:robolectric:3.2.2'
    testImplementation 'org.apache.commons:commons-lang3:3.5'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    // Android JUnit Runner
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    // JUnit4 Rules
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
}
