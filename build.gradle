buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
    }
}


apply plugin: 'com.android.application'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().substring(0, 8).trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "com.tastybug.timetracker"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 100
        versionName "1.00"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    lintOptions {
        abortOnError true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
    signingConfigs {
        release {
            storeFile file("timetracker.keystore")
            storePassword TIMETRACKER_RELEASE_STORE_PASSWORD // ~/.gradle/gradle.properties
            keyAlias "timetracker"
            keyPassword TIMETRACKER_RELEASE_KEY_PASSWORD // ~/.gradle/gradle.properties
        }
    }
    buildTypes {
        debug {
            buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
            resValue "bool", "test_data_generator_accessible", 'true'
        }
        release {
            buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
            resValue "bool", "test_data_generator_accessible", 'false'
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"),
                    "proguard-rules.pro"
        }
    }
}

dependencies {
    compile 'com.google.guava:guava:20.0'
    compile 'com.squareup:otto:1.3.4'
    compile 'net.danlew:android.joda:2.9.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'commons-io:commons-io:2.5'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.13'
    testCompile 'org.robolectric:robolectric:3.2.2'
    testCompile 'org.apache.commons:commons-lang3:3.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    // Android JUnit Runner
    androidTestCompile 'com.android.support.test:runner:0.5'
    // JUnit4 Rules
    androidTestCompile 'com.android.support.test:rules:0.5'

}
